group 'io.advantageous.reakt'
version '2.1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'


ext {
    vertxVersion = '3.2.1'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

test.onlyIf { !Boolean.getBoolean('skip.tests') }


repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile group: project.group, name: 'reakt', version: "2.1.0-SNAPSHOT"
    compile group: 'io.vertx', name: 'vertx-core', version: vertxVersion

}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required false
    sign configurations.archives
}



uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                try {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                } catch (MissingPropertyException ignore) {
                }
            }

            pom.project {
                packaging 'jar'
                name project.name
                description "Bridge from Reakt to Vertx to provide promises."

                url 'https://github.com/advantageous/${project.name}'

                scm {
                    url 'scm:git@github.com:advantageous/${project.name}.git'
                    connection 'scm:git@github.com:advantageous/${project.name}.git'
                    developerConnection 'scm:git@github.com:advantageous/${project.name}.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'richardHightower'
                        name 'Richard Hightower'
                    }
                    developer {
                        id 'sailorgeoffrey'
                        name 'Geoffrey Chandler'
                    }
                }
            }
        }
    }
}


